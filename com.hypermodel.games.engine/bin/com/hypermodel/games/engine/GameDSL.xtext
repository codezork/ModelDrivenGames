grammar com.hypermodel.games.engine.GameDSL with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate gameDSL "http://www.hypermodel.com/games/engine/GameDSL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

GameModel:
	packages+=GamePackage*;

GameImport:
	'import' importedNamespace=GameQualifiedNameWithWildCard;
	 
GamePackage:
	'package' name=QualifiedName '{' 
		(imports+=GameImport)*
		config=GamePlatformConfig
		(games+=GameRoot)*
	'}';
	
GamePlatformConfig: {GamePlatformConfig}
	'configuration' '{' 
	android = GamePlatformConfigAndroid
	ios = GamePlatformConfigIOS
	iosmoe = GamePlatformConfigIOSMOE
	'}';
	
GamePlatformConfigAndroid: {GamePlatformConfigAndroid}
	'android' '{'
	'minAndroidVersion' minAndroidVersion=INT
	'targetAndroidVersion' targetAndroidVersion=INT
	'SDKApiLevel' apiLevel=INT
	'orientation' orientation=GameScreenOrientation
	(('icon' appIcon=STRING)? &
	('name' appName=STRING)? &
	('label' appLabel=STRING)? )
	(useAccelerometer?='useAccelerometer')? 
	'}' 
;
	
GamePlatformConfigIOS: {GamePlatformConfigIOS}
	'ios' '{'
	(useAccelerometer?='useAccelerometer')? '}'
;

GamePlatformConfigIOSMOE: {GamePlatformConfigIOSMOE}
	'iosmoe' '{'
	(useAccelerometer?='useAccelerometer')? '}'
;

GamePlatformConfiguration:
	'configure' platform=GamePlatform '{' config+=GamePlatformConfigAndroid* '}';

GameRoot:
	{GameRoot} 'game' name=ID
	'width' width = INT 'height' height = INT 'title' title=STRING 'pixelPerMeter' ppm = INT
	('scenes' '{' scenes+=GameScene* '}'&
	'screens' '{' screens+=GameScreen* '}'&
	'regions' '{' regions+=GameTextureRegion* '}');
	
GameScreen:
	{GameScreen} 'screen' name=ID 
	'atlas' atlasName = STRING
	'scene' scene = [GameScene]
	'sprites' '{' sprites+=GameSprite* '}';
	
GameScene:
	{GameScene} 'scene' name=ID 
	(hasScore?='isScore' score = GameScore)?;
		
GameScore:
	{GameScore} 'topPadding' topPadding=INT 
	'{' displays+=GameDisplay* '}';
	
GameDisplay:
	{GameDisplay} 'display' name=ID 
	'valueType' type=GameDisplayValueType 
	'format' format=STRING 
	(hasInitial?='initialValue' (initialNumberValue=SignedNumber|initialIntValue=INT|initialStringValue=STRING))?
	(hasDelta?='deltaValue' deltaValue=SignedNumber 'eachTimePeriod' timePeriod=UnsignedNumber)?
	(hasAdder?='addValue')?
	(hasSetter?='setValue')?;
	
GameSprite:
	{GameSprite} 'sprite' name=ID 'id' id=INT 
	'radius' radius=INT
	('animations' '{' animations+=GameSpriteAnimation* '}')?
	'stands' '{' stands+=GameSpriteStand* '}'
	'startStand' start=[GameSpriteStand]
	'startPosition' 'x' x=INT 'y' y=INT
	(hasSensor?='sensorLength' sensorLength=INT 'sensorID' sensorID=INT)?
	('interactsWith' '{' interactionSprites+=[GameSprite]* '}')?;

GameSpriteAnimation:
	{GameSpriteAnimation} 'animation' name=ID 'duration' duration=UnsignedNumber 
	(hasFrames?='frames' frames=INT 'startFrame' offset=INT 'from' region=[GameTextureRegion])?
	(hasStands?='stands' '{' stands+=GameSpriteStand '}')?;
	
GameSpriteStand:
	{GameSpriteStand} 'stand' name=ID 'whichFrame' offset=INT 'from' region=[GameTextureRegion];

GameTextureRegion:
	{GameTextureRegion} 'region' name=ID 'width' width=INT 'height' height=INT;

enum GameDisplayValueType:
	int = 'number'|
	float = 'decimal'|
	String = 'text';
	
ValidID:
	ID;
	
QualifiedName:
	ValidID (=>'.' ValidID)*;
  
SignedNumber returns ecore::EFloat:
	('-')? UnsignedNumber;

UnsignedNumber returns ecore::EFloat:
	INT ('.' (INT));
	
GameQualifiedNameWithWildCard:
	QualifiedName ('.' '*')?;
	
enum GamePlatform:
	android 	= 'android'|
	desktop 	= 'desktop'|
	html 		= 'html'|
	ios 		= 'ios'|
	iosmoe 		= 'ios-moe';
	
enum GameScreenOrientation:
	landscape = 'landscape'|
	portrait = 'portait';